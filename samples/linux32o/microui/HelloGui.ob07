MODULE HelloGui;
IMPORT Args,
	RL IN "./Raylib.ob07",
	MUI IN "./MicroUI.ob07",
	MURL IN "./MURL.ob07";


PROCEDURE start();
VAR
	bgColor: RL.Color;
	fontSize: INTEGER;
	ctx: MUI.Context;
	wr1, wr2: MUI.Rect;
	font: RL.Font;
	widths2: ARRAY 1 OF INTEGER;
	buf2: ARRAY 32 OF CHAR;
	svalue1, svalue2: REAL;
	title: ARRAY 32 OF CHAR;
BEGIN

	RL.IntToColor(RL.WHITE, bgColor);
	fontSize := 16;
	wr1.x := 20; wr1.y := 20; wr1.w := 200; wr1.h := 150;
	wr2.x := 300; wr2.y := 20; wr2.w := 200; wr2.h := 200;
	widths2[0] := -1;
	font := RL.CreateFont();
	
	IF Args.argc > 1 THEN
		Args.GetArg(1, title);
	ELSE
		title := "HelloGui - microui";
	END;
	
	RL.InitWindow(800, 600, title);
	RL.SetTargetFPS(60);
	
	MUI.Init(ctx);
	RL.LoadFontEx("./resources/anonymous_pro_bold.ttf", font, 11);
	//MURL.SetupFont(ctx);
	MURL.SetupFontEx(ctx, font);
	
	WHILE ~ RL.WindowShouldClose() DO
		MURL.HandleInput(ctx);
		MUI.Begin(ctx);
		
		IF MUI.BeginWindowEx(ctx, "Hello", wr1, MUI.OPT_NOCLOSE) THEN
			MUI.Label(ctx, "Hello, raylib");
			
			IF MUI.Button(ctx, "The button") THEN
				MUI.OpenPopup(ctx, "popup");
			END;
			
			IF MUI.BeginPopup(ctx, "popup") THEN
				MUI.Label(ctx, "This is a popup");
				MUI.EndPopup(ctx);
			END;

			MUI.EndWindow(ctx);
		END;
		
		IF MUI.BeginWindowEx(ctx, "Font", wr2, MUI.OPT_NORESIZE + MUI.OPT_NOTITLE) THEN
			MUI.LayoutRow(ctx, 1, widths2, 0);
			MUI.Label(ctx, "Hello, World");
			
			IF MUI.Textbox(ctx, buf2, 32) THEN
			END;
			
			IF MUI.Slider(ctx, svalue1, 0.0, 100.00) THEN
			END;
			
			IF MUI.SliderEx(ctx, svalue2, 0.0, 100.00, 1.0, "%.0f", MUI.OPT_NONE) THEN
			END;
			
			
			MUI.EndWindow(ctx);
		END;
	
		MUI.End(ctx);
		
		RL.BeginDrawing();
			RL.ClearBackground(bgColor);
			MURL.Render(ctx);
		RL.EndDrawing();
	END;
	
	MUI.Destroy(ctx);
	RL.DestroyFont(font);
	RL.CloseWindow();

END start;


BEGIN

	start();

END HelloGui.
