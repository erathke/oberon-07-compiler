MODULE Coroutines;
IMPORT SYSTEM, Out := Out_sys;

CONST 
	IDLE* = 0;
	RUNNING* = 1;
	SUSPENDED* = 2;
	DEAD* = 4;
	
TYPE
	UINT32 = INTEGER;
	PID* = INTEGER;
	
	Userdata* = POINTER TO RECORD
	END;
	
	Context* = POINTER TO ContextDesc;
	
	Handler* = PROCEDURE (ctx: Context; r: INTEGER): INTEGER;
	
	ContextDesc = RECORD
		csp, stkbase: UINT32;
		stksize: INTEGER;
		handler: Handler;
		user*: Userdata;
		status: INTEGER
	END;
	


PROCEDURE [extern, "coroutine_init32"] coro_init(ctx: UINT32; handlerAddr: UINT32; stackAddr: UINT32);
PROCEDURE [extern, "coroutine_yield32"] coro_yield(ctx: UINT32; data: UINT32): INTEGER;
//PROCEDURE [extern, "coroutine_yield32"] coro_resume(ctx: UINT32; data: UINT32): INTEGER;



PROCEDURE LogContext(VAR ctx: Context);
VAR
	addr: UINT32;
BEGIN
	
	Out.String("=================="); Out.Ln;
	Out.String("sp  : "); Out.Int(ctx.csp, 0); Out.Ln;
	Out.String("base: "); Out.Int(ctx.stkbase, 0); Out.Ln;
	Out.String("size: "); Out.Int(ctx.stksize, 0); Out.Ln;
	addr := SYSTEM.VAL(ctx.handler, UINT32);
	Out.String("handler: "); Out.Int(addr, 0); Out.Ln;
	addr := SYSTEM.VAL(ctx.user, UINT32);
	Out.String("user: "); Out.Int(addr, 0); Out.Ln;
	Out.String("=================="); Out.Ln;
END LogContext;


PROCEDURE [ccall] EntryPoint(ctxAddr: UINT32; r: INTEGER): INTEGER;
VAR
	ctx: Context;
	value: INTEGER;
BEGIN
	ctx := SYSTEM.VAL(ctxAddr, Context);
	value := ctx.handler(ctx, r);
	ctx.status := DEAD;
	RETURN value
END;


PROCEDURE Create*(handler: Handler; VAR stack: ARRAY OF BYTE; data: Userdata): Context;
VAR
	ctx: Context;
	baseAddr, spAddr, spIdx : UINT32;
	handlerAddr: UINT32;
	stksize: INTEGER;
BEGIN
	stksize := LEN(stack);
	baseAddr := SYSTEM.ADR(stack[0]);
	spIdx := stksize - SYSTEM.SIZE(ContextDesc);
	spAddr := SYSTEM.ADR(stack[spIdx]);
	ctx := SYSTEM.VAL(spAddr, Context);
	
	ctx.csp := spAddr;
	ctx.stkbase := baseAddr;
	ctx.stksize := stksize;
	ctx.handler := handler;
	ctx.user := data;
	
	handlerAddr := SYSTEM.ADR(EntryPoint);
	coro_init(spAddr, handlerAddr, spAddr);
	
	RETURN ctx
END Create;


PROCEDURE Yield*(ctx: Context; r: INTEGER): INTEGER;
VAR
	value: INTEGER;
	addr: UINT32;
BEGIN
	
	IF ctx.status = RUNNING THEN
		addr := SYSTEM.VAL(ctx, UINT32);
		ctx.status := SUSPENDED;
		value := coro_yield(addr, r);
	ELSE
		CASE ctx.status OF
		| DEAD: Out.String("cannot yield dead coroutine"); Out.Ln;
		| IDLE: Out.String("coroutine not running"); Out.Ln;
		END;
	END;
	
	RETURN value
END Yield;


PROCEDURE Resume*(ctx: Context; r: INTEGER): INTEGER;
VAR
	value: INTEGER;
	addr: UINT32;
BEGIN
	IF (ctx.status = IDLE) OR (ctx.status = SUSPENDED) THEN
		ctx.status := RUNNING;
		addr := SYSTEM.VAL(ctx, UINT32);
		value := coro_yield(addr, r);
	ELSE
		CASE ctx.status OF
		| DEAD: Out.String("cannot resume dead coroutine"); Out.Ln;
		END;
	END;
	RETURN value
END Resume;
	
END Coroutines.
